#!/bin/perl

# i3-clam-shell --- turn laptop display on/off with lid switch events

# Copyright (C) 2024 Daniel Hennigar

# This program is free software; you can redistribute it and/or modify
# it under the terms of either:

#   a) the Artistic License 2.0, or
#   b) the GNU General Public License as published by the Free Software
#      Foundation; either version 3, or (at your option) any later version.

# See the LICENSE file for more information.

use 5.040;
use Carp;
use English      qw(-no_match_vars);
use Getopt::Long qw(:config auto_help auto_version);
use i3::Utils;

sub VERSION {
    return $i3::Utils::VERSION;
}

GetOptions() or croak 'Could not parse options';

my $msg = 'i3-msg';
if ( exists $ENV{'SWAYSOCK'} ) {
    $msg = 'swaymsg';
}

my $lid_state_path = '/proc/acpi/button/lid/LID/state';
open my $lid_state_fh, '<', $lid_state_path
  or croak 'Could not open lid state file';
my $lid_state = <$lid_state_fh>;
close $lid_state_fh
  or croak 'Could not close lid state file';

if ( $lid_state =~ m/open/xms ) {
    system "$msg output eDP-1 enable";
}
else {
    system "$msg output eDP-1 disable";
}

__END__

=pod

=head1 NAME

i3-clam-shell - a simple script to ensure that laptop screens stay on/off when i3/sway are in "clamshell mode", even after a reload.

=head1 SYNOPSIS

  i3-clam-shell [options]

=head1 DESCRIPTION

Triggered by a i3/sway reload, this script will ensure that the displays are powered on and off as appropriate for a "clamshell mode", i.e., when the laptop lid is close the internal monitor should stay powered off. Example configuration:

=over 4

C<exec_always i3-clam-shell>

=back

=head1 OPTIONS

=over 4

=item B<--help>

Print usage information

=item B<--version>

Print version information

=back

=head1 AUTHOR

Daniel Hennigar <danrhennigar@gmail.com>

=head1 LICENSE AND COPYRIGHT

Copyright (C) 2025 Daniel Hennigar 

Licensed under the Artistic License 2.0 and GNU GPL.

=cut
