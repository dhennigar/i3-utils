#!/usr/bin/perl

# i3-new-workspace.pl --- open the next available empty workspace

# Copyright (c) 2024 Daniel Hennigar

# This program is free software; you can redistribute it and/or modify
# it under the terms of either:
#
#   a) the Artistic License 2.0, or
#   b) the GNU General Public License as published by the Free Software
#      Foundation; either version 3, or (at your option) any later version.
#
# See the LICENSE file for more information.

use 5.040;
use strict;
use warnings;
use Getopt::Std;
use AnyEvent::I3;

my $take = 0;

my %opts;
getopts( 'ht', \%opts );
if ( $opts{h} ) {
    say "Usage: $0 [options]";
    say "See perldoc $0 for documentation.";
    exit 0;
}
$take = 1 if $opts{t};

my $workspaces = i3->get_workspaces->recv;
my %workspace_numbers;
foreach my $workspace (@$workspaces) {
    $workspace_numbers{ $workspace->{num} } = 1;
}

my $lowest_empty_workspace;
my $max_workspace_number = scalar @$workspaces + 1;
for my $i ( 1 .. $max_workspace_number ) {
    unless ( exists $workspace_numbers{$i} ) {
        $lowest_empty_workspace = $i;
        last;
    }
}

if ( defined $lowest_empty_workspace ) {
    if ($take) {
        i3->command( "move to workspace "
              . $lowest_empty_workspace
              . "; workspace "
              . $lowest_empty_workspace );
    }
    else {
        i3->command( "workspace " . $lowest_empty_workspace );
    }
}

__END__

=pod

=head1 NAME

i3-new-workspace - Open the lowest available empty workspace

=head1 SYNOPSIS

  i3-new-workspace [--take] [--help]

=head1 DESCRIPTION

Find the lowest numbered workspace with no windows and switch to it. Optionally, take the currently focused window with you to that workspace.

=head1 OPTIONS

=over 4

=item * --help

=item * --take - bring the current window with you

=back

=head1 AUTHOR

Daniel Hennigar

=head1 LICENSE

Licensed under the Artistic License 2.0 and GNU GPL.

=cut
