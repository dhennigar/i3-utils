#!/usr/bin/env perl

# i3-cycle-focus --- emulate "Alt-Tab" behaviour in sway/i3

# Copyright (C) 2024 Daniel Hennigar

# Author: Daniel Hennigar

# This program is free software; you can redistribute it and/or modify
# it under the terms of either:

#   a) the Artistic License 2.0, or
#   b) the GNU General Public License as published by the Free Software
#      Foundation; either version 3, or (at your option) any later version.

# See the LICENSE file for more information.

use 5.032;
use strict;
use warnings;
use Getopt::Long;
use AnyEvent::I3;
use i3::Utils qw(find_node find_nodes);

my $usage = "Usage: $0 [--reverse] [--help]\n";
my $reverse = 0;
my $help = 0;
GetOptions(
    'reverse' => \$reverse,
    'help' => \$help
) or die "$usage";
die "$usage" if $help || @ARGV;

my $tree = i3->get_tree->recv;

my $is_focused = sub { $_[0]->{focused} };
my ( $focused, undef, $workspace ) = find_node( $tree, $is_focused );

my $has_name = sub { $_[0]->{name} && $_[0]->{name} ne '' };
my @siblings = find_nodes( $workspace, $has_name );
@siblings = reverse @siblings if $reverse;

# Find current window in list of siblings and focus the next one
my $index = 0;
$index++ while $siblings[$index]{id} != $focused->{id};
my $next_id = $siblings[ ( $index + 1 ) % @siblings ]{id};
say "next id: $next_id";
i3->command("[con_id=$next_id] focus");
