#!/usr/bin/env perl

# sway-cycle-focus.pl --- emulate "Alt-Tab" behaviour in sway/i3

# Copyright (C) 2024 Daniel Hennigar

# Author: Daniel Hennigar

# This program is free software; you can redistribute it and/or modify
# it under the terms of either:

#   a) the Artistic License 2.0, or
#   b) the GNU General Public License as published by the Free Software
#      Foundation; either version 3, or (at your option) any later version.

# See the LICENSE file for more information.

use 5.032;
use strict;
use warnings;
use AnyEvent::I3;
use i3::Utils qw(find_node find_nodes);

# Get tree data from i3/sway
my $tree = i3->get_tree->recv;

# Find the current container and its workspace
my $is_focused = sub {
    my ($node) = @_;
    return $node->{focused};
};
my ( $focused, $parent, $workspace ) = find_node( $tree, $is_focused );

# Find all the workspace's named child nodes
my $has_name = sub {
    my ($node) = @_;
    return ( $node->{name} && $node->{type} eq 'con' );
};
my $siblings = find_nodes( $workspace, $has_name );

# Iterate through list of siblings and focus the one after currently focused
my $index = 0;
$index++ while $siblings->[$index]->{id} != $focused->{id};
my $next_id = $siblings->[ ( $index + 1 ) % @$siblings ]->{id};
say "next id: $next_id";
i3->command("[con_id=$next_id] focus");
